AC_REVISION([$Id$])dnl

dnl Process this file with autoconf to produce a configure script.


AC_INIT
AC_CONFIG_SRCDIR([libecs/Entity.hpp])

AM_CONFIG_HEADER(config.h)
AC_SYS_LARGEFILE
ECELL_MAJOR_VERSION=3
ECELL_MINOR_VERSION=1
ECELL_MICRO_VERSION=104
VERSION=$ECELL_MAJOR_VERSION.$ECELL_MINOR_VERSION.$ECELL_MICRO_VERSION
PACKAGE=ecell
ECELL_VERSION_STRING=$VERSION


AC_DEFINE_UNQUOTED( ECELL_MAJOR_VERSION, ${ECELL_MAJOR_VERSION},[] )
AC_DEFINE_UNQUOTED( ECELL_MINOR_VERSION, ${ECELL_MINOR_VERSION},[] )
AC_DEFINE_UNQUOTED( ECELL_MICRO_VERSION, ${ECELL_MICRO_VERSION},[] )
AC_DEFINE_UNQUOTED( ECELL_VERSION_STRING, "${ECELL_VERSION_STRING}",[] )

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

AM_SANITY_CHECK

AC_CANONICAL_HOST

if test $prefix = "NONE" ; then
	PREFIX=$ac_default_prefix
else
	PREFIX=$prefix
fi

AC_SUBST(PREFIX)

dnl Checks for programs.

AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

AM_DISABLE_STATIC
dnl AC_LIBLTDL_INSTALLABLE(../libltdl)
AC_LIBLTDL_CONVENIENCE(../libltdl)
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL


rm -f conftest
./libtool --config > conftest
. ./conftest
rm -f conftest
dnl
dnl compatibility for libtool 1.5.6
LTDL_SHLIB_EXT=""
if test -n "$shrext_cmds"; then
    LTDL_SHLIB_EXT=$shrext_cmds
    AC_SUBST(LTDL_SHLIB_EXT)
dnl compatibility for libtool 1.5.0
elif test -n "$shrext"; then
    LTDL_SHLIB_EXT=$shrext
    AC_SUBST(LTDL_SHLIB_EXT)
dnl compatibility for libtool 1.4.x
else
  AC_CACHE_CHECK([which extension is used for shared libraries],
    libltdl_cv_shlibext, [dnl
  (
    last=
    for spec in $library_names_spec; do
      last="$spec"
    done
  changequote(, )
    echo "$last" | sed 's/\[.*\]//;s/^[^.]*//;s/\$.*$//;s/\.$//' > conftest
  changequote([, ])
  )
  libltdl_cv_shlibext=`cat conftest`
  rm -f conftest
  ])
  if test -n "$libltdl_cv_shlibext"; then
    LTDL_SHLIB_EXT=$libltdl_cv_shlibext
    AC_SUBST(LTDL_SHLIB_EXT)
  fi
fi



AM_PATH_PYTHON(2.2)
AC_SUBST(PYTHON)
PYTHON_VERSION=${am_cv_python_version}
PYTHON_WIN_VERSION=${am_cv_python_version/'.'}
AC_SUBST(PYTHON_VERSION)
AC_SUBST(PYTHON_WIN_VERSION)

AC_LANG([C++])

dnl Checks for libraries.

dnl assuming libc and libm always present...

AC_CHECK_LIB(m, sin, LIBM="-lm")
AC_SUBST(LIBM)
dnl
AC_CHECK_LIB(c, open, LIBC="-lc")
AC_SUBST(LIBC)
dnl

dnl disable checking dlopen for windows
case ${host_os} in
  *cygwin* | *mingw* | *win32*)
    os_win32=yes
    AM_CHECK_PYMOD(py2exe,,,[AC_MSG_ERROR([could not find py2exe module])])
    ;;
  *)
    AC_CHECK_LIB(dl, dlopen, LIBDL="-ldl")
    if test "$LIBDL" = ""; then
      AC_MSG_ERROR([The libdl could not be found.])
    fi
    AC_SUBST(LIBDL) 
    os_win32=no
    ;;
esac
AM_CONDITIONAL(OS_WIN32, test "$os_win32" = "yes")


AC_CHECK_LIB(gslcblas,cblas_dgemm,,[AC_MSG_ERROR([The libgslcblas (part of GNU Scientific Library) could not be found.])])

AC_CHECK_LIB(gsl,gsl_block_alloc,,[AC_MSG_ERROR([The libgsl (GNU Scientific Library) could not be found.])])

AM_CHECK_PYMOD(Numeric,,,[AC_MSG_ERROR([could not find Python Numeric module])])
AM_CHECK_PYMOD(xml,,,[AC_MSG_ERROR([could not find Python xml module])])

dnl Checks for header files.

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h)
dnl
dnl stl
dnl
dnl AC_CHECK_HEADER(stl.h,,
dnl 	AC_MSG_ERROR([stl.h not found.])
dnl )
AC_CHECK_HEADERS(math,,)
AC_CHECK_HEADERS(cmath,,)


dnl
dnl
EXT_GUESS="../../../.. ../../.. ../.. ../ ./ $prefix /usr /usr/local /opt"
dnl
dnl
dnl dmtool package
dnl
if test "$DMTOOL_DIR" = "" ; then
        AC_MSG_CHECKING( for dmtool dir)
        ac_cv_dmtool_dir="no"
        for ac_dir in $EXT_GUESS ; do
                if test -d $ac_dir/dmtool ; then
                        ac_cv_dmtool_dir=`(cd $ac_dir ; pwd)`
			DMTOOL_DIR=$ac_cv_dmtool_dir
                fi
        done

	if test "$DMTOOL_DIR" = "" ; then	
		AC_MSG_ERROR("dmtool not found.")
	fi

        AC_MSG_RESULT($ac_cv_dmtool_dir)
        DMTOOL_DIR=$ac_cv_dmtool_dir
        DMTOOL_INCLUDE_DIR=$ac_cv_dmtool_dir
fi
AC_SUBST(DMTOOL_DIR)
AC_SUBST(DMTOOL_INCLUDE_DIR)

dnl
dnl
dnl boost package
dnl
dnl
AC_ARG_WITH( boost-root-dir,,
  [BOOST_ROOT_DIR="$withval"],
  [BOOST_ROOT_DIR=])
EXT_GUESS="$BOOST_ROOT_DIR $EXT_GUESS "

AC_MSG_CHECKING( for boost include dir );
BOOST_INCLUDE=""	
for ac_dir in $EXT_GUESS ; do
    if test "$BOOST_INCLUDE" = "" ; then
        if test -f $ac_dir/include/boost/python.hpp ; then
            BOOST_INCLUDE=`(cd $ac_dir/include ; pwd)`
            BOOST_LIB=`(cd $ac_dir/lib ; pwd)`
        elif test -f $ac_dir/include/boost*/boost/python.hpp ; then
            BOOST_INCLUDE=`(cd $ac_dir/include/boost* ; pwd)`
            BOOST_LIB=`(cd $ac_dir/lib ; pwd)`
        fi
    fi
done
if test "$BOOST_INCLUDE" = "" ; then
    AC_MSG_ERROR(boost includes not found);
else
    BOOST_INCLUDE="-I$BOOST_INCLUDE"
    BOOST_LIB="-L$BOOST_LIB"
    BOOST_PYTHON_LIB="-lboost_python"
fi
AC_MSG_RESULT($BOOST_INCLUDE)
AC_SUBST(BOOST_INCLUDE)
AC_SUBST(BOOST_PYTHON_LIB)
AC_SUBST(BOOST_LIB)

AM_CHECK_PYTHON_HEADERS(,[AC_MSG_ERROR([])])


AC_MSG_CHECKING( if use of compiler specific language features are allowed )

AC_ARG_ENABLE( compiler-extensions,,
	       USE_COMPILER_EXTENSIONS=$enable_compiler_features,
	       USE_COMPILER_EXTENSIONS=yes
	       )
AC_MSG_RESULT( $USE_COMPILER_EXTENSIONS )

if test "$USE_COMPILER_EXTENSIONS" != no ; then
   AC_MSG_CHECKING( for special compiler options )
   AC_SUBST(USE_COMPILER_EXTENSIONS)
   AC_DEFINE([USE_COMPILER_EXTENSIONS],1,[make use of compiler specific language features])
   CXXFLAGS="$CXXFLAGS -Wno-pmf-conversions"
   AC_SUBST(CXXFLAGS)
   AC_MSG_RESULT( $CXXFLAGS )
fi
   


AC_LANG_PUSH([C])

AC_C_CONST
AC_C_INLINE
if test "$ac_cv_c_inline" != no ; then
  AC_DEFINE([HAVE_INLINE],1,[define if inline is supported])
  AC_SUBST(HAVE_INLINE)
fi

AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_C_LONG_DOUBLE
AC_LANG_POP([])

AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)
AC_CHECK_SIZEOF(double)
AC_CHECK_SIZEOF(long double)


dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(mkdir select strdup strerror strtod uname modf modfl)
AC_CHECK_FUNCS(strdup strstr)

AC_CONFIG_FILES([pyecs/ecell3-python], [chmod +x pyecs/ecell3-python])
AC_CONFIG_FILES([pyecell/ecell3-eml2sbml], [chmod +x pyecell/ecell3-eml2sbml])
AC_CONFIG_FILES([pyecell/ecell3-sbml2eml], [chmod +x pyecell/ecell3-sbml2eml])
AC_CONFIG_FILES([pyecell/ecell3-session], [chmod +x pyecell/ecell3-session])
AC_CONFIG_FILES([pyecell/gecell3-session], [chmod +x pyecell/gecell3-session])
AC_CONFIG_FILES([pyecell/ecell3-session-manager],
                [chmod +x pyecell/ecell3-session-manager])
AC_CONFIG_FILES([Makefile
                 libecs/Makefile
                 libemc/Makefile
                 pyecs/Makefile
                 pyecs/windows/Makefile
                 pyecs/windows/ecell3-installer.iss
                 pyecs/windows/pathVariables
                 pyecell/Makefile
                 pyecell/ecell/Makefile
                 pyecell/ecell/SessionManager/Makefile
                 dm/Makefile])
AC_OUTPUT
