; Inno Setup script to create E-Cell Simulation Environment Version 3 Installer
; Author: Satya Arjunan < satya@sfc.keio.ac.jp >

[Setup]
AppName=E-Cell Simulation Environment Version 3
AppVerName=E-Cell Simulation Environment Version 3
AppPublisher=Keio University
AppPublisherURL=http://ecell.sourceforge.net
AppSupportURL=http://ecell.sourceforge.net
AppUpdatesURL=http://ecell.sourceforge.net
DefaultDirName=C:\E-Cell3
DefaultGroupName=E-Cell 3
SetupIconFile="ecell.ico"
UninstallDisplayIcon={app}\bin\gecell.exe
UninstallDisplayName=E-Cell Simulation Environment @VERSION@
ChangesAssociations=yes
OutputBaseFilename=ecell-@VERSION@-setup
OutputDir=.
SolidCompression=yes
Compression=none
;Compression=lzma/ultra

[Tasks]
Name: "desktopicon"; Description: "Create a &desktop icon"; GroupDescription: "Additional icons:"; MinVersion: 4,4

[Files]
Source: "ecell3-python.bat"; DestDir: "{app}"; CopyMode: alwaysoverwrite
Source: "ecell3-console.bat"; DestDir: "{app}"; CopyMode: alwaysoverwrite
Source: "ecell.ico"; DestDir: "{app}"; CopyMode: alwaysoverwrite
Source: "ecell3-console.ico"; DestDir: "{app}"; CopyMode: alwaysoverwrite
Source: "bin\*.*"; DestDir: "{app}\bin"; CopyMode: alwaysoverwrite; Flags: recursesubdirs
Source: "etc\*.*"; DestDir: "{app}\etc"; CopyMode: alwaysoverwrite; Flags: recursesubdirs
Source: "include\*.*"; DestDir: "{app}\include"; CopyMode: alwaysoverwrite; Flags: recursesubdirs
Source: "lib\*.*"; DestDir: "{app}\lib"; CopyMode: alwaysoverwrite; Flags: recursesubdirs
Source: "mingw32\*.*"; DestDir: "{app}\mingw32"; CopyMode: alwaysoverwrite; Flags: recursesubdirs
Source: "python@PYTHON_WIN_VERSION@\*.*"; DestDir: "{app}\python@PYTHON_WIN_VERSION@"; CopyMode: alwaysoverwrite; Flags: recursesubdirs
Source: "share\*.*"; DestDir: "{app}\share"; CopyMode: alwaysoverwrite; Flags: recursesubdirs

[Icons]
Name: "{group}\E-Cell3"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\E-Cell3 Console"; Filename: "{app}\ecell3-console.bat"; WorkingDir: "{app}"; IconFilename: "{app}\ecell3-console.ico"
Name: "{group}\Samples\simple"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\simple\simple.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\Samples\branchG"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\branchG\branchG.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\Samples\CoupledOscillator"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\CoupledOscillator\cascade.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\Samples\Drosophila"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\Drosophila\Drosophila.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\Samples\Drosophila-cpp"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\Drosophila-cpp\Drosophila.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\Samples\Heatshock"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\Heatshock\heatshock.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\Samples\LTD"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\LTD\LTD.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\Samples\Pendulum"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\Pendulum\Pendulum.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\Samples\SSystem"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\SSystem\SSystem.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\Samples\Toy_Hybrid"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell -f share\doc\sample\Toy_Hybrid\Toy_Hybrid.eml"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized
Name: "{group}\E-Cell3 Documentation\API Reference Manual"; Filename: "{app}\share\doc\refman\refman.pdf"; WorkingDir: "{app}\share\doc\refman"
Name: "{group}\E-Cell3 Documentation\API Reference Manual (html)"; Filename: "{app}\share\doc\refman\html\main.html"; WorkingDir: "{app}\share\doc\refman\html"
Name: "{group}\E-Cell3 Documentation\User's Manual"; Filename: "{app}\share\doc\users-manual\ecell3-users-manual.pdf"; WorkingDir: "{app}\share\doc\users-manual"
Name: "{group}\E-Cell3 Documentation\User's Manual (html)"; Filename: "{app}\share\doc\users-manual\ecell3-users-manual\book1.html"; WorkingDir: "{app}\share\doc\users-manual\ecell3-users-manual"
Name: "{userdesktop}\E-Cell3"; Filename: "{app}\ecell3-python.bat"; Parameters: "gecell"; WorkingDir: "{app}"; IconFilename: "{app}\ecell.ico"; Flags: runminimized; MinVersion: 4,4; Tasks: desktopicon
Name: "{userdesktop}\E-Cell3 Console"; Filename: "{app}\ecell3-console.bat";  WorkingDir: "{app}"; IconFilename: "{app}\ecell3-console.ico"; MinVersion: 4,4; Tasks: desktopicon

[Registry]
;Registry entry for ECELL3_PREFIX
;Root: HKLM; Subkey: "Software\E-Cell3"; Flags: deletekey uninsdeletekey
;Root: HKLM; Subkey: "Software\E-Cell3\Settings"; ValueType: string; ValueName: "ECELL3_PREFIX"; ValueData: "{app}"

;File association for *.eml
Root: HKCR; Subkey: ".eml"; ValueType: string; ValueName: ""; ValueData: "EcellModelDescriptor"; Flags: createvalueifdoesntexist uninsdeletevalue
;.eml" is the extension we're associating. "EcellModelDescriptor" is the internal name for the file type as stored in the registry.
;Make sure you use a unique name for this so you don't inadvertently overwrite another application's registry key.
Root: HKCR; Subkey: "EcellModelDescriptor"; ValueType: string; ValueName: ""; ValueData: "E-Cell Model Descriptor File"; Flags: createvalueifdoesntexist uninsdeletekey
;"E-Cell Model Descriptor" above is the name for the file type as shown in Explorer.
Root: HKCR; Subkey: "EcellModelDescriptor\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\gecell.exe,0"
;"DefaultIcon" is the registry key that specifies the filename containing the icon to associate with the file type.
;",0" tells Explorer to use the first icon from gecell.exe. (",1" would mean the second icon.)
Root: HKCR; Subkey: "EcellModelDescriptor\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\gecell.exe"" -f ""%1"""
;"shell\open\command" is the registry key that specifies the program to execute when a file of the type is double-clicked in Explorer.
;The surrounding quotes are in the command line so it handles long filenames correctly.

;File association for *.em
Root: HKCR; Subkey: ".em"; ValueType: string; ValueName: ""; ValueData: "EcellModelFile"; Flags: createvalueifdoesntexist uninsdeletevalue
Root: HKCR; Subkey: "EcellModelFile"; ValueType: string; ValueName: ""; ValueData: "E-Cell Model File"; Flags: createvalueifdoesntexist uninsdeletekey
Root: HKCR; Subkey: "EcellModelFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\ecell3-em2eml.exe,0"
Root: HKCR; Subkey: "EcellModelFile\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\ecell3-em2eml.exe"" ""%1"""

;File association for *.ess
Root: HKCR; Subkey: ".ess"; ValueType: string; ValueName: ""; ValueData: "EcellSessionScript"; Flags: createvalueifdoesntexist uninsdeletevalue
Root: HKCR; Subkey: "EcellSessionScript"; ValueType: string; ValueName: ""; ValueData: "E-Cell Session Script File"; Flags: createvalueifdoesntexist uninsdeletekey
Root: HKCR; Subkey: "EcellSessionScript\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\ecell3-session.exe,0"
Root: HKCR; Subkey: "EcellSessionScript\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\ecell3-session.exe"" -e ""%1"""

;File association for *.esm
Root: HKCR; Subkey: ".esm"; ValueType: string; ValueName: ""; ValueData: "EcellSessionManager"; Flags: createvalueifdoesntexist uninsdeletevalue
Root: HKCR; Subkey: "EcellSessionManager"; ValueType: string; ValueName: ""; ValueData: "E-Cell Session Manager Script"; Flags: createvalueifdoesntexist uninsdeletekey
Root: HKCR; Subkey: "EcellSessionManager\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\ecell3-session-manager.exe,0"
Root: HKCR; Subkey: "EcellSessionManager\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\ecell3-session-manager.exe"" -e ""%1"""

;File association for *.cpp
Root: HKCR; Subkey: ".cpp"; ValueType: string; ValueName: ""; ValueData: "EcellDMSource"; Flags: createvalueifdoesntexist uninsdeletevalue
Root: HKCR; Subkey: "EcellDMSource\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\ecell3-dmc.exe ""%1"" ""%2"" ""%3"" ""%4"" ""%5"" ""%6"" ""%7"" ""%8"" ""%9"""



